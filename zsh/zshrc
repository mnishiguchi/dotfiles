## Helpers

command_exists() { command -v "$1" &>/dev/null; }

## ZSH configuration

# https://github.com/ohmyzsh/ohmyzsh/blob/f4435a4522c1070b04d4e3a5b6b705bcebc309e5/lib/completion.zsh
unsetopt menu_complete # do not autoselect the first completion entry
unsetopt flowcontrol
setopt auto_menu # show completion menu on successive tab press
setopt complete_in_word
setopt always_to_end

# https://github.com/ohmyzsh/ohmyzsh/blob/f4435a4522c1070b04d4e3a5b6b705bcebc309e5/lib/history.zsh
#setopt extended_history      # record timestamp of command in HISTFILE
setopt hist_expire_dups_first # delete duplicates first when HISTFILE size exceeds HISTSIZE
setopt hist_ignore_dups       # ignore duplicated commands history list
setopt hist_ignore_space      # ignore commands that start with space
setopt hist_verify            # show command with history expansion to user before running it
setopt share_history          # share command history data

autoload -U compinit
compinit

# https://github.com/ohmyzsh/ohmyzsh/blob/f4435a4522c1070b04d4e3a5b6b705bcebc309e5/lib/key-bindings.zsh#L109
bindkey '^r' history-incremental-search-backward # [Ctrl-r] - Search backward incrementally for a specified string. The string may begin with ^ to anchor the search to the beginning of the line.

## Oh my zsh

# Uninstall oh-my-zsh
# https://github.com/ohmyzsh/ohmyzsh#uninstalling-oh-my-zsh
command_exists uninstall_oh_my_zsh && uninstall_oh_my_zsh

## ZSH autosuggestions

# https://github.com/zsh-users/zsh-autosuggestions
autosuggestions_dir="$XDG_CONFIG_HOME/zsh/zsh-autosuggestions"

if [ ! -d "$autosuggestions_dir" ]; then
  git clone --depth=1 https://github.com/zsh-users/zsh-autosuggestions.git "$autosuggestions_dir"
fi

source "$autosuggestions_dir/zsh-autosuggestions.zsh"

## ZSH syntax-highlighting

# https://github.com/zsh-users/zsh-syntax-highlighting
syntax_highlighting_dir="$XDG_CONFIG_HOME/zsh/zsh-syntax-highlighting"

if [ ! -d "$syntax_highlighting_dir" ]; then
  git clone --depth=1 https://github.com/zsh-users/zsh-syntax-highlighting.git "$syntax_highlighting_dir"
fi

source "$syntax_highlighting_dir/zsh-syntax-highlighting.zsh"

## zoxide

# https://github.com/ajeetdsouza/zoxide
if ! command_exists zoxide; then
 curl -sS https://raw.githubusercontent.com/ajeetdsouza/zoxide/main/install.sh | bash
fi

eval "$(zoxide init zsh)"

## asdf

ASDF_DIR="${ASDF_DIR:-$HOME/.asdf}"

# https://asdf-vm.com/guide/getting-started.html
if [ -f "$ASDF_DIR/asdf.sh" ]; then
  # Load command
  source "$ASDF_DIR/asdf.sh"

  # Load completions
  if [ -f "${ASDF_DIR}/completions/_asdf" ]; then
    # append completions to fpath
    fpath=(${ASDF_DIR}/completions $fpath)
    # initialise completions with ZSH's compinit
    autoload -Uz compinit && compinit
  fi
fi

## direnv

# https://direnv.net/docs/hook.html
command_exists direnv && eval "$(direnv hook zsh)"

## Shell prompt

# https://starship.rs/config/
export STARSHIP_CONFIG="$XDG_CONFIG_HOME/zsh/starship.toml"

# https://starship.rs
if ! command_exists starship; then
  curl -sS https://starship.rs/install.sh | sh
fi

eval "$(starship init zsh)"

## Common shell stuff

source "${XDG_CONFIG_HOME}/shell/aliases"
source "${XDG_CONFIG_HOME}/shell/functions"

## Wrapping up

# Clean up PATH
PATH=$(printf "%s" "$PATH" | awk -v RS=':' '!a[$1]++ { if (NR > 1) printf RS; printf $1 }')

# Do this at the very end so that we can tell all above is executed
pcall neofetch
